plugins {
    id "io.quarkus" version "${quarkusPlatformVersion}"
    id 'org.jetbrains.kotlin.jvm' version "${kotlinVersion}"
    id 'org.jetbrains.kotlin.plugin.allopen' version "${kotlinVersion}"
    id 'org.jetbrains.kotlin.plugin.serialization' version "${kotlinVersion}"
    id "org.jetbrains.kotlin.kapt" version "${kotlinVersion}"
    id 'org.openapi.generator' version '7.20.0'
    id 'java'
    id 'idea'
    id 'application'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation enforcedPlatform("io.quarkus:quarkus-bom:${quarkusPlatformVersion}")
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-config-yaml'
    implementation 'io.quarkus:quarkus-container-image-docker'
    implementation 'io.quarkus:quarkus-flyway'
    implementation 'io.quarkus:quarkus-jdbc-postgresql'
    implementation 'io.quarkus:quarkus-kotlin'
    implementation 'io.quarkus:quarkus-oidc'
    implementation 'io.quarkus:quarkus-rest'
    implementation 'io.quarkus:quarkus-rest-jackson'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-smallrye-openapi'

    implementation 'jakarta.validation:jakarta.validation-api:3.1.1'
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposedVersion"
    implementation "org.jetbrains.exposed:exposed-dao:$exposedVersion"
    implementation "org.jetbrains.exposed:exposed-java-time:$exposedVersion"
    implementation 'org.openapitools:jackson-databind-nullable:0.2.9'

    implementation "org.mapstruct:mapstruct:$mapstructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"
    kapt "org.mapstruct:mapstruct-processor:$mapstructVersion"

    implementation 'org.jetbrains.kotlin:kotlin-stdlib'

    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.rest-assured:rest-assured'
}

group 'io.kay'
version '1.0.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

kotlin {
    compilerOptions {
        freeCompilerArgs = ['-Xjvm-default=all-compatibility']
    }
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}

allOpen {
    annotation("jakarta.ws.rs.Path")
    annotation("kotlinx.serialization.Serializable")
    annotation("jakarta.enterprise.context.ApplicationScoped")
    annotation("jakarta.persistence.Entity")
    annotation("io.quarkus.test.junit.QuarkusTest")
}

openApiGenerate {
    generatorName = 'jaxrs-spec'
    inputSpec = "${rootDir}/src/main/resources/META-INF/openapi.yaml"
    outputDir = "${buildDir}/openapi"
    apiPackage = "io.kay.website.api"
    modelPackage = "io.kay.website.api.model"
    configOptions = [
            dateLibrary          : "java8",
            library              : "quarkus",
            useJakartaEe         : "true",
            interfaceOnly        : "true",
            serializableModel    : "true",
            useSwaggerAnnotations: "false",
    ]
}

quarkusGenerateDevAppModel.dependsOn('openApiGenerate')
compileKotlin.dependsOn('openApiGenerate')

import org.jetbrains.kotlin.gradle.internal.KaptGenerateStubsTask

tasks.withType(KaptGenerateStubsTask).configureEach {
    dependsOn 'openApiGenerate'
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/openapi/src/gen/java"
        }
    }
}
